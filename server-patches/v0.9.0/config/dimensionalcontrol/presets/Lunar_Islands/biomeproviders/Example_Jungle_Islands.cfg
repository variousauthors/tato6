#Set all biomes to lunar maria
genLayer = new GenLayerBaseSingle(1, "biometweaker:lunar_maria")

#Randomly replace lunar maria with 8 touching lunar maria with lunar highlands
#This creates islands of lunar highlands in the maria
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_maria", null, 8, "biometweaker:lunar_highlands", 3)

#Zoom out in order to enlarge lunar highlands biomes
genLayer = magnify(1000, genLayer, 5)

#Randomly replace lunar maria touching lunar highlands with lunar highlands. This is to add some texture to the hill biome shape.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands", 5)

#The rest of the rules just grow the highlands a bit rougher

#Replace all lunar maria touching lunar highlands with more highlands. This surrounds lunar highlands completely with highlands.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands")

#Randomly replace lunar maria touching highlands with more highlands. This adds texture to the highlands biome shape.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands", 5)



#Zoom out in order to enlarge lunar highlands biomes
genLayer = magnify(1000, genLayer, 3)

#BLUE ZONE Big Crater

# replace highlands surrounded by 8 highlands with crater floor
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_highlands", null, 8, "biometweaker:lunar_crater_peak", 1000)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_peak", null, 1, "biometweaker:lunar_blue_zone", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_blue_zone", null, 2, "biometweaker:lunar_blue_zone", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_blue_zone", null, 2, "biometweaker:lunar_blue_zone", 1)

#Add ejecta

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_blue_zone", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Add the crater rim

#Replace all ejecta touching crater floow with crater rim. This surrounds crater completely with crater rim.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_blue_zone", null, 1, "biometweaker:lunar_crater_rim_large")

#Replace all ejecta touching crater rim with highlands for that blast ring effect
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_crater_rim_large", null, 1, "biometweaker:lunar_highlands")

#End BLUE ZONE Big Crater

#Big Craters

# replace highlands or ejecta surrounded by 8 highlands with crater floor
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_highlands", null, 8, "biometweaker:lunar_crater_peak", 500)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_peak", null, 1, "biometweaker:lunar_crater_floor_large", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_floor_large", null, 2, "biometweaker:lunar_crater_floor_large", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_floor_large", null, 2, "biometweaker:lunar_crater_floor_large", 1)

#Add the ejecta

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor_large", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Zoom out so that the crater rim and ejecta is not so fat
genLayer = magnify(1000, genLayer, 1)

#Add the crater rim

#Replace all ejecta touching crater floow with crater rim. This surrounds crater completely with crater rim.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_crater_floor_large", null, 1, "biometweaker:lunar_crater_rim_large")

#Replace all ejecta touching crater rim with highlands for that blast ring effect
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_crater_rim_large", null, 1, "biometweaker:lunar_highlands")

#END Big Craters

#Replace all lunar maria touching highlands with highlands edge. This surrounds the highlands with highlands edge.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands")

#Randomly replace lunar maria touching highlands edge with highlands edge. This adds texture to the highlands edge biome shape.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands", 5)

## THIS IS WHERE THE ZOOM OUT USED TO BE ##

#Randomly replace lunar maria touching highlands edge with beach. Doing this with only a 1/2 chance twice allows for most of the shore to be covered while still allowing there to be some highlands edge without beach.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands", 2)
genLayer = new GenLayerTouching(1001, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_highlands", 2)

#Replace all lunar maria touching highlands edge or beach with ocean. This surrounds the highlands edge and beach with ocean. Repeated once for a larger ocean area.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_maria")
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_highlands", null, 1, "biometweaker:lunar_maria")

#Replace all ocean touching 6 or more highlands edge or biometweaker:lunar_highlands biomes with highlands edge. This is to clean up the edges a bit, make sure inland ocean areas don't happen too often.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", ["biometweaker:lunar_highlands", "biometweaker:lunar_highlands"], null, 6, "biometweaker:lunar_highlands")

#Replace all lunar maria touching ocean with ocean. This is to increase the size of the ocean area without increasing the size of the beach area.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", 0, null, 1, "biometweaker:lunar_maria")

#Zoom out to increase island size
genLayer = magnify(1000, genLayer, 1)

# now add craters inside the highlands

# replace highlands or ejecta surrounded by 8 highlands with crater floor
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], ["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], null, 8, "biometweaker:lunar_crater_peak", 500)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_peak", null, 1, "biometweaker:lunar_crater_floor", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_floor", null, 2, "biometweaker:lunar_crater_floor", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_floor", null, 2, "biometweaker:lunar_crater_floor", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 1)

genLayer = magnify(1000, genLayer, 1)

#Replace all lunar highlands touching crater floow with crater rim. This surrounds crater completely with crater rim.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_crater_floor", null, 1, "biometweaker:lunar_crater_rim")

# now add craters inside the highlands

# replace highlands or ejecta surrounded by 8 highlands with crater floor
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor_large", "biometweaker:lunar_crater_ejecta"], ["biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor_large", "biometweaker:lunar_crater_ejecta"], null, 8, "biometweaker:lunar_crater_peak", 1000)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor_large", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_peak", null, 1, "biometweaker:lunar_crater_floor", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor_large", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_floor", null, 2, "biometweaker:lunar_crater_floor", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData(["biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor_large", "biometweaker:lunar_crater_ejecta"], "biometweaker:lunar_crater_floor", null, 2, "biometweaker:lunar_crater_floor", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_floor", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 1)

# add small craters to the maria

# replace highlands or ejecta surrounded by 8 highlands with crater floor
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_maria", null, 8, "biometweaker:lunar_crater_peak", 2000)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_peak", null, 1, "biometweaker:lunar_crater_floor", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_floor", null, 2, "biometweaker:lunar_crater_floor", 1)

# replace all highlands touching crater floor with crater floor to expand the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_floor", null, 2, "biometweaker:lunar_crater_floor", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_floor", null, 1, "biometweaker:lunar_crater_ejecta", 1)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 1)

genLayer = magnify(1000, genLayer, 1)

#Add the ring of ejecta around the highlands craters

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_highlands", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Add the ring of ejecta around the maria craters

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Replace some lunar highlands touching crater floor with ejecta, to make rays out from the crater
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_maria", "biometweaker:lunar_crater_ejecta", null, 1, "biometweaker:lunar_crater_ejecta", 2)

#Add the crater rim

#Replace all ejecta touching crater floow with crater rim. This surrounds crater completely with crater rim.
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_crater_floor", null, 1, "biometweaker:lunar_crater_rim")

#Replace all ejecta touching crater rim with highlands for that blast ring effect
genLayer = new GenLayerTouching(1000, genLayer)
genLayer.addData("biometweaker:lunar_crater_ejecta", "biometweaker:lunar_crater_rim", null, 1, "biometweaker:lunar_highlands")

#Add voronoi zoom layer to the end of the chain for use as the second variable in the setGenLayers() call. This is always required.
finalGenLayer2 = new GenLayerVoronoiZoom(10, genLayer)

#Save the GenLayer chain. The first variable is used for terrain generation. The second variable is zoomed in 4x and is used for all other biome lookups.
setGenLayers(genLayer, finalGenLayer2)

